#!/usr/bin/env expect -f

proc telnet_connect {} {
    global user passwd
    expect {
        timeout { send_user "\nFailed to get login prompt\n"; exit 1 }
        eof { send_user "\ntelnet failure\n"; exit 1 }
        "login:"
    } 
    send "$user\r"
    expect {
        timeout { send_user "\nFailed to get password prompt\n"; exit 1 }
        eof { send_user "\ntelnet failure\n"; exit 1 }
        "Password:"
    } 
    send "$passwd\r"
    expect {
        timeout { send_user "\nFailed to get shell prompt\n"; exit 1 }
        eof { send_user "\ntelnet failure\n"; exit 1 }
        "\# "
    } 
}

proc get_dirs {path} {
    global basedir
    set filenames {}
    set fp $basedir/$path
    send "FP=$fp; test -d \$FP -o -f \$FP && ls -1p \$FP\r"
    expect "\r\n"
    expect {
        timeout { send_user "\nFailed to get shell prompt\n"; exit 1 }
        eof { send_user "\ntelnet failure\n"; exit 1 }
        -re "(\[a-zA-z0-9\./]+)\r\n" { set filename $expect_out(1,string); lappend filenames $filename; exp_continue }
        "\# " {}
    } 
    return $filenames
}

proc is_dir_path {p} {
    set lastchar [string index $p [expr [string length $p] - 1]]
    return [string equal $lastchar "/"]
}

proc get_all_files {path} {
    set files {}
    set lslist [get_dirs $path]
    foreach p $lslist {
        if { [is_dir_path $p] } {
            set files [concat $files [get_all_files $path/$p]]
        } else {
            lappend files $path/$p
        }
    }
    return $files
}

proc telnet_disconnect {} {
    send "exit\r"
    expect eof
}

#############################################
# Starting here

#read the input parameters

set host dcs-942l
set user root
set passwd 110684sa
set basedir /mnt/usb/dcs-942l
set path [lindex $argv 0]

log_user 0
spawn telnet $host
telnet_connect
set filenames [get_all_files $path]
foreach fn $filenames {
    send_user "$fn\n" 
}
telnet_disconnect
exit 0
